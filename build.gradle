plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

group 'net.suqatri.redicloud'
version '1.0.1-' + getCommitHash() + '-SNAPSHOT'

tasks.register('buildAndCopy', Copy) {
    dependsOn projectBuild
    finalizedBy ':copyToTest'
}

tasks.register('copyToTest') {
    println 'Deleting test files'
    project.delete(
            file("${rootDir}/test/node-1/node-base-${project.version}.jar"),
            file("${rootDir}/test/node-1/storage/plugin-proxy-${project.version}.jar"),
            file("${rootDir}/test/node-1/storage/plugin-minecraft-${project.version}.jar")
    )
    println 'Copying files'
    dependsOn copyNodeBase
    dependsOn copyMinecraftPlugin
    dependsOn copyProxyPlugin
}

tasks.register('projectBuild') {
    dependsOn ':commons:build'
    dependsOn ':api:api:build'
    dependsOn ':api:api-default-impl:build'

    dependsOn ':commands:command-locales:build'
    dependsOn ':commands:command-core:build'
    dependsOn ':commands:command-bukkit:build'
    dependsOn ':commands:command-bungeecord:build'

    dependsOn ':api:api-node:build'
    dependsOn ':api:api-minecraft:build'
    dependsOn ':api:api-proxy:build'

    dependsOn ':node:node-base:build'
    dependsOn ':node:node-runner:build'

    dependsOn ':plugins:plugin-minecraft:build'
    dependsOn ':plugins:plugin-proxy:build'
}

tasks.register('copyNodeBase', Copy) {
    from "node/node-base/build/libs/node-base-${project.version}-all.jar"
    into 'test/node-1'
    rename "node-base-${project.version}-all.jar", "node-base-${project.version}.jar"
}

tasks.register('copyMinecraftPlugin', Copy) {
    from "plugins/plugin-minecraft/build/libs/plugin-minecraft-${project.version}-all.jar"
    into 'test/node-1/storage'
    rename "plugin-minecraft-${project.version}-all.jar", "plugin-minecraft-${project.version}.jar"
}

tasks.register('copyProxyPlugin', Copy) {
    from "plugins/plugin-proxy/build/libs/plugin-proxy-${project.version}-all.jar"
    into 'test/node-1/storage'
    rename "plugin-proxy-${project.version}-all.jar", "plugin-proxy-${project.version}.jar"
}

tasks.register('publishToRepository') {
    dependsOn ':commons:publish'
    dependsOn ':api:api:publish'
    dependsOn ':api:api-default-impl:publish'

    dependsOn ':commands:command-locales:publish'
    dependsOn ':commands:command-core:publish'
    dependsOn ':commands:command-bukkit:publish'
    dependsOn ':commands:command-bungeecord:publish'

    dependsOn ':api:api-node:publish'
    dependsOn ':api:api-minecraft:publish'
    dependsOn ':api:api-proxy:publish'

    dependsOn ':node:node-base:publish'
    dependsOn ':node:node-runner:publish'

    dependsOn ':plugins:plugin-minecraft:publish'
    dependsOn ':plugins:plugin-proxy:publish'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    version '1.0.1-' + getCommitHash() + '-SNAPSHOT'

    sourceCompatibility = '1.8'

    compileJava {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven {
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            url = "https://repo1.maven.org/maven2/"
        }
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url = "https://repo.aikar.co/nexus/content/groups/aikar/"
        }
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        maven {
            url = "https://repo1.maven.org/maven2/"
        }
    }

    dependencies {
        implementation('org.projectlombok:lombok:1.18.24')
        annotationProcessor('org.projectlombok:lombok:1.18.24')

        implementation('org.redisson:redisson:3.17.4')
        implementation('com.google.guava:guava:31.1-jre')
        implementation('org.jetbrains:annotations:23.0.0')
    }

    tasks.jar.dependsOn tasks.shadowJar

    shadowJar {
        archiveFileName.set("redicloud-${project.name}.jar")
        dependencies {
            configurations = [project.configurations.runtimeClasspath]
        }
        relocate 'io.netty', 'net.suqatri.redicloud.libs.io.netty'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifact("build/libs/redicloud-${project.name}.jar") {
                    extension 'jar'
                }
            }
        }
        repositories {
            maven {
                name 'nexus'
                url getRepoUrl()
                allowInsecureProtocol true
                credentials {
                    username project.repoUser
                    password project.repoPassword
                }
            }
        }
    }
}

def isRelease() {
    return project.version.contains('-RELEASE')
}

def getRepoUrl(){
    if(isRelease()){
        return 'http://repo.suqatri.net:8081/repository/maven-releases/'
    }else{
        return 'http://repo.suqatri.net:8081/repository/maven-snapshots/'
    }
}

def getCommitHash() {
    def git = new ProcessBuilder('git', 'rev-parse', getCurrentBrancheName()).directory(project.projectDir).start()
    def inputStream = git.getInputStream();
    def reader = new BufferedReader(new InputStreamReader(inputStream))
    def commitHash = reader.readLine()
    git.waitFor()
    println("Commit hash: " + commitHash)
    return commitHash
}

def getCurrentBrancheName(){
    def git = new ProcessBuilder('git', 'symbolic-ref', '--short', '-q', 'HEAD').directory(project.projectDir).start()
    def inputStream = git.getInputStream();
    def reader = new BufferedReader(new InputStreamReader(inputStream))
    def branchName = reader.readLine()
    git.waitFor()
    println('Current branch: ' + branchName)
    return branchName
}